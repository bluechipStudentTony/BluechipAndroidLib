apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'

apply plugin: "com.jfrog.bintray"

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'



version "1.0.1"

android {
    compileSdkVersion 27
    defaultConfig {

        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}


def siteUrl = 'https://github.com/bluechiptony/BluechipAndroidLib'      // Homepage URL of the library
def gitUrl = 'https://github.com/bluechiptony/BluechipAndroidLib.git'   // Git repository URL
group = "com.tonyegwu"                                          // Maven Group ID for the artifact



install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'com.tonyegwu.bluechiptony:bluechipandroidlib'
                description 'Bluechip basid android library'
                url siteUrl

                licenses{
                    license{
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'bluechiptony'
                        name 'Anthony Egwu'
                        email 'tony5egwul@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }

            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())



// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "bluechipandroidlib"
        // it is the name that appears in bintray when logged
        name = "jumpstart" // TODO
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT license"]
        publish = true
        version {
            gpg {
                sign = false //Determines whether to GPG sign the files. The default is false

            }
        }
    }
}